buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

group groupName
version versionName

repositories {
    mavenCentral()
    maven {
        url "https://dl.bintray.com/memtrip/maven/"
    }
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.6"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.6"
}

jar.baseName = "${rootProject.name}"


publishing {
    publications {
        maven(MavenPublication) {
            artifact(project.tasks.jar) {
                artifactId "${project.name}"
                extension 'jar'
            }


            pom.withXml {
                asNode().appendNode('name', "${project.name}")
                asNode().appendNode('description', "${description}")
                def dependenciesNode = asNode().appendNode('dependencies')

                def adder = {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }

                configurations.implementation.allDependencies.each(adder)
            }
        }
    }
}


dependencies {
    kapt project(path: ":processor", configuration: 'default')
    kapt project(path: ':eos-abi-writer-preprocessor', configuration: 'default')

    implementation project(path: ":annotations", configuration: 'default')
    api project(path: ":eos-core", configuration: 'default')
    api project(path: ":abi-implementation", configuration: 'default')
    api project(path: ":shared-model", configuration: 'default')
    api project(path: ":eos-http-rpc", configuration: 'default')
    api project(path: ":eos-chain-actions", configuration: 'default')
    api project(path: ":eos-abi-writer", configuration: 'default')

    api "com.squareup.okhttp3:logging-interceptor:$loggingIntrceptorVersion"
    implementation 'net.gcardone.junidecode:junidecode:0.4.1'
    implementation "com.squareup.moshi:moshi-adapters:$moshiAdapterVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiCodegenVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiKotlinVersion"
    implementation("org.bitcoinj:bitcoinj-core:$bitcoinJVersion") {
        exclude group: "com.lambdaworks", module: "scrypt"
    }
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    testImplementation 'junit:junit:4.12'
}
